# $Id$
# Maintainer:  <voobscout@archlinux.info>
# Contributor: chrisaw <home@chrisaw.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-lib-gpd-pocket
pkgver=1.1.5
_srcname=alsa-lib-${pkgver}
pkgrel=1
pkgdesc='An alternative implementation of Linux sound support WITH GPD Pocket patches'
arch=('x86_64')
url='http://www.alsa-project.org'
depends=('glibc')
optdepends=('python2: for python smixer plugin')
conflicts=('alsa-lib')
provides=("alsa-lib=${pkgver}")
makedepends=('python2')
license=('LGPL')
source=(
  "ftp://ftp.alsa-project.org/pub/lib/${_srcname}.tar.bz2"
  'HiFi.conf'
)
sha256sums=(
  'f4f68ad3c6da36b0b5241ac3c798a7a71e0e97d51f972e9f723b3f20a9650ae6'
  'SKIP'
)

prepare() {
  cp HiFi.conf ${_srcname}/src/conf/ucm/chtrt5645/HiFi.conf
}

build() {
  cd ${_srcname}
  
  ./configure --without-debug --prefix=/usr --with-pythonlibs="-lpthread -lm -ldl -lpython2.7" --with-pythonincludes=-I/usr/include/python2.7
  
  make
}

package() {
  cd ${_srcname}
  
  make DESTDIR="${pkgdir}" install
}
